@PACKAGE_INIT@

set_and_check(CUSAN_INCLUDE_DIR "@PACKAGE_INCLUDE_INSTALL_DIR@")
set_and_check(CUSAN_LIBRARY_DIR "@PACKAGE_LIBRARY_INSTALL_DIR@")
set_and_check(CUSAN_BINARY_DIR "@PACKAGE_BINARY_INSTALL_DIR@")
set_and_check(CUSAN_CMAKE_DIR "@PACKAGE_CMAKE_INSTALL_DIR@")

macro (check_components comps_list)
  set(cusan_FOUND true)

  foreach(component ${comps_list})
    if(NOT TARGET cusan::${component} AND NOT TARGET cusan::${component}Static)
      set(cusan_${component}_FOUND false)
      if(cusan_FIND_REQUIRED_${component})
        message(WARNING "Could not find component ${component}")
        set(cusan_FOUND false)
      endif()
    else()
      set(cusan_${component}_FOUND true)
    endif()
  endforeach()
endmacro()

if (cusan_FIND_COMPONENTS)
  foreach(component ${cusan_FIND_COMPONENTS})
    if(EXISTS ${CMAKE_CURRENT_LIST_DIR}/cusan{component}Targets.cmake)
      include(${CMAKE_CURRENT_LIST_DIR}/cusan{component}Targets.cmake)
    endif()
  endforeach()
  check_components("${cusan_FIND_COMPONENTS}")
else()
  file(GLOB
    target-file
    LIST_DIRECTORIES false
    "${CMAKE_CURRENT_LIST_DIR}/cusan*Targets.cmake"
  )
  foreach(component ${target-file})
    include(${component})
  endforeach()
endif()
